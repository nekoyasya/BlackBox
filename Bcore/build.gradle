apply plugin: 'com.android.library'

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion

        consumerProguardFiles "consumer-rules.pro"
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('./src/main/cpp/CMakeLists.txt')
        }
    }
    dexOptions {
        preDexLibraries false
        maxProcessCount 8
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lint {
        abortOnError false
        checkOnly 'NewApi', 'InlinedApi'
        checkReleaseBuilds false
        disable 'UnusedResources', 'RestrictedApi'
        textOutput file('stdout')
        textReport false
        warningsAsErrors false
    }
    namespace 'top.niunaijun.blackbox'
}


tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}


dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation 'me.weishu:free_reflection:3.0.1'
    implementation "com.github.CodingGay.BlackReflection:core:$rootProject.ext.blackReflection"
    implementation project(':Bcore:black-fake')
    implementation project(':Bcore:black-hook')
    implementation project(':android-mirror')

    implementation project(':Bcore:pine-core')
    implementation project(':Bcore:pine-xposed')
    implementation project(':Bcore:pine-xposed-res')
}